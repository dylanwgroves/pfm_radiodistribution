/* Basics ______________________________________________________________________

Project: Wellspring Tanzania, Radio Distribution Experiment
Purpose: Analysis
Author: dylan groves, dylanwgroves@gmail.com
Date: 2023/12/12
________________________________________________________________________________*/


/* Introduction ________________________________________________________________*/
	
	clear all	
	clear matrix
	clear mata
	set more off
	global c_date = c(current_date)
	tempfile dta_main 
	tempfile dta_vill
	

/* Run Prelim File _____________________________________________________________	

	do "${code}/pfm_.master/00_setup/pfm_paths_master.do"
	do "${code}/pfm_ne/pfm_ne_prelim.do"
*/


/* Set table name ______________________________________________________________*/

	local tablename "${rd_clean_tables}/pfm_rd_tables_results_pknow.tex"
	
	
/* Load Data ___________________________________________________________________*/	

	import excel "${rd_tables}/pfm_rd_analysis_main_all.xlsx", sheet("pknow") firstrow clear
	save `dta_main', replace
	
	
/* Sort Data ___________________________________________________________________*/

	/* Individual Data */
	sort pval
	destring 	variable variablelabel coef se pval r2 N ///
				lasso_coef lasso_se lasso_pval ripval lasso_ripval lasso_r2 lasso_N lasso_ctls lasso_ctls_num ///
				treat_mean treat_sd ctl_mean ctl_sd vill_sd min max, replace
	save `dta_main', replace


/* Create Rank _________________________________________________________________*/

	/* Individual Data */
	# d ;
	local sumstat_vars 	
						pknow_vp
						pknow_ruto
						pknow_ports
						pknow_justice
						pknow_pm
						pknow_em
						pknow_kenyatta
						;
	#d cr 

	gen rank = .
	local i = 1
	
	foreach var of local sumstat_vars {
		replace rank = `i' if variable == "`var'"
		local i = `i' + 1
	}		   

	drop if rank == .
	gsort rank

	
/* Variable lables _____________________________________________________________*/
	
	tostring variablelabel, replace
	replace variablelabel = "Port Deal" if variable == 						"pknow_ports"
	replace variablelabel = "Vice Prez" if variable == 						"pknow_vp"
	replace variablelabel = "Kenya Prez" if variable == 					"pknow_ruto"
	replace variablelabel = "Kenya Prez" if variable == 					"pknow_kenyatta"
	replace variablelabel = "Justice" if variable == 						"pknow_justice"
	replace variablelabel = "Prime Min." if variable == 					"pknow_pm"
	replace variablelabel = "EM Ruling" if variable == 						"pknow_em"


/* Create variables ____________________________________________________________*/

	count
	global count = `r(N)'
	
	forval i = 1/$count {
	
		/* Drop pre-existing macros */
		macro drop var`i' b`i' se`i' p`i' r2`i' n`i' ctl`i' c`i' sd`i' mn`i' mx`i'

		global var`i' = variablelabel[`i']
		
		/* Normal */
		global b`i' = strofreal(coef[`i'], "%6.3f")
		global se`i' = strofreal(se[`i'], "%6.3f")
		global p`i' = strofreal(ripval[`i'], "%6.3f")
		global r`i' = strofreal(r2[`i'], "%6.2f")
		global n`i' = strofreal(N[`i'], "%6.0fc")
		global ctl`i' = "No"
		
		/* Lasso */
		global b`i'c = strofreal(lasso_coef[`i'], "%6.3f")
		global se`i'c = strofreal(lasso_se[`i'], "%6.3f")
		global p`i'c = strofreal(lasso_ripval[`i'], "%6.3f")
		global r`i'c = strofreal(lasso_r2[`i'], "%6.2f")
		global n`i'c = strofreal(lasso_N[`i'], "%6.0fc")
		global ctl`i'c = strofreal(lasso_ctls_num[`i'], "%6.0fc")
		
		/* Summary stats */
		global c`i' = strofreal(ctl_mean[`i'], "%6.2f")
		global sd`i' = strofreal(ctl_sd[`i'], "%6.2f")
		global mn`i' = strofreal(min[`i'], "%6.0f")
		global mx`i' = strofreal(max[`i'], "%6.0f")
	
	}
		
	/* Assign stars and set p-value to <0.001 instead of 0.000 */
	do "${code}/pfm_radiodistribution/01_helpers/pfm_rd_helper_stars.do"	
	
	
/* Make Table __________________________________________________________________*/	

texdoc init "`tablename'", replace nolog

texdoc write 	{
texdoc write 	\def\sym#1{\ifmmode^{#1}\else\(^{#1}\)\fi}
texdoc write 	\begin{tabular}{l*{16}{c}}
texdoc write 	\Xhline{2\arrayrulewidth}\addlinespace[3pt]	

texdoc write    							& \multicolumn{3}{c}{2022-23} 						&   \multicolumn{4}{c}{2019-2022}  \tstrut \bstrut \\ \cmidrule(r){2-4} \cmidrule(r){5-8} 

texdoc write    							& $var1				& $var2    		& $var3 		& $var4 		& $var5			& $var6			& $var7				\\ 
texdoc write 	\hline
texdoc write 	Treatment      				& $b1\sym{$s1}   	& $b2\sym{$s2}  & $b3\sym{$s3} 	& $b4\sym{$s4}	& $b5\sym{$s5}	& $b6\sym{$s6}	& $b7\sym{$s7}		\\
texdoc write    Standard Error 				& $se1				& $se2    		& $se3 			& $se4 			& $se5			& $se6			& $se7				\\
texdoc write 	\hline
texdoc write 	RI \$p\$-value 				& $p1				& $p2			& $p3  			& $p4			& $p5			& $p6			& $p7				\\
texdoc write 	Hypothesis					& +					& +				& +				& +				& +				& +				& +					\\
texdoc write 	Control Mean      			& $c1				& $c2			& $c3  			& $c4			& $c5			& $c6			& $c7				\\
texdoc write 	Control SD 					& $sd1				& $sd2			& $sd3 			& $sd4			& $sd5			& $sd6			& $sd7				\\
texdoc write	DV Range					& [$mn1-$mx1]		& [$mn2-$mx2]	& [$mn3-$mx3] 	& [$mn4-$mx4]	& [$mn5-$mx5] 	& [$mn6-$mx6] 	& [$mn7-$mx7] 		\\	
texdoc write 	Blocked FE 					& Yes 				&  Yes   		& Yes			& Yes			& Yes			& Yes			& Yes				\\
texdoc write 	Controls 					& $ctl1 			&  $ctl2   		& $ctl3			& $ctl4			& $ctl5			& $ctl6			& $ctl7				\\
texdoc write 	Adj-\$ R^2\$				& $r1 				& $r2   		& $r3			& $r4			& $r6			& $r6			& $r7				\\
texdoc write 	Observations      			& $n1 				& $n2   		& $n3			& $n4			& $n6			& $n6			& $n7				\\
texdoc write 	\Xhline{2\arrayrulewidth}
texdoc write 	\end{tabular}
texdoc write 	}

texdoc close








